#Область PushОтправка

&НаСервере
Функция ОтправитьPUSH_Android(ИдентификаторПодписчика, Title, Data, Text) Экспорт
	Попытка
		
		ИдентификаторПодписчикаОбъект = nsОбщегоНазначения.ДесериализоватьXDTO(ИдентификаторПодписчика);
		
		СоединениеHTTP = Новый HTTPСоединение("fcm.googleapis.com",443,,,,, Новый ЗащищенноеСоединениеOpenSSL(,));
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Authorization", "key=" + Константы.nsFCMКлючCервера.Получить());
		Заголовки.Вставить("Content-Type", "application/json");
		
		АдресРесурса = "fcm/send";
		
		ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		
		ТелоСообщения = Новый Структура;
        ТелоСообщения.Вставить("title", Title);
        ТелоСообщения.Вставить("data", Data);        
        ТелоСообщения.Вставить("text", Text);        
                                                                                                                           
        ПараметрыСообщения = Новый Структура;
        ПараметрыСообщения.Вставить("to", ИдентификаторПодписчикаОбъект.ИдентификаторУстройства);
        ПараметрыСообщения.Вставить("data", ТелоСообщения);
		
		СообщениеJSON = nsОбщегоНазначения.ЗаписатьДанныеВJSON(ПараметрыСообщения);
		Если СообщениеJSON = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли; 
		
		ЗапросHTTP.УстановитьТелоИзСтроки(СообщениеJSON, "UTF-8");
		Ответ = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP);
		
		Если НЕ Ответ.КодСостояния = 200 Тогда
			nsОбработкаОшибок.ЗаписатьОшибку("Ошибка отправки PUSH сообщения. КодСостояния = " + Ответ.КодСостояния + Символы.ПС + Ответ.ПолучитьТелоКакСтроку(), "PushСервер\ОтправитьPUSH_Android");
		КонецЕсли; 
		
		Возврат Ответ.КодСостояния;
	Исключение
		Возврат nsОбработкаОшибок.ЗаписатьОшибку(ОписаниеОшибки());
	КонецПопытки;
	
КонецФункции

&НаСервере
Функция ОтправитьPUSH_iOS(ИдентификаторПодписчика, Title, Data, Text) Экспорт
	Попытка
		
		СоединениеHTTP = Новый HTTPСоединение(Константы.nsАдресPushСервиса.Получить());
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		
		АдресРесурса = "hs/push/ios";
		
		ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		
        ПараметрыСообщения = Новый Структура;
        ПараметрыСообщения.Вставить("Title", Title);        
        ПараметрыСообщения.Вставить("Data", Data);        
        ПараметрыСообщения.Вставить("Text", Text);        
        ПараметрыСообщения.Вставить("Recipient", ИдентификаторПодписчика);
		
		СообщениеJSON = nsОбщегоНазначения.ЗаписатьДанныеВJSON(ПараметрыСообщения);
		Если СообщениеJSON = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли; 
		
		ЗапросHTTP.УстановитьТелоИзСтроки(СообщениеJSON, "UTF-8");
		Ответ = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP);
		
		Если НЕ Ответ.КодСостояния = 200 Тогда
			nsОбработкаОшибок.ЗаписатьОшибку("Ошибка отправки PUSH сообщения. КодСостояния = " + Ответ.КодСостояния + Символы.ПС + Ответ.ПолучитьТелоКакСтроку(), "PushСервер\ОтправитьPUSH_iOS");
		КонецЕсли; 
		
		Возврат Ответ.КодСостояния;
	Исключение
		Возврат nsОбработкаОшибок.ЗаписатьОшибку(ОписаниеОшибки());
	КонецПопытки;
	
КонецФункции

#КонецОбласти 

#Область Регистр

Функция ПолучитьИдентификаторПодписчикаОбъект(МобильноеУстройство) Экспорт
	
	ИдентификаторПодписчикаXML = ПолучитьИдентификаторПодписчикаXML(МобильноеУстройство);
	Если ИдентификаторПодписчикаXML = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	ИдентификаторПодписчикаОбъект = nsОбщегоНазначения.ДесериализоватьXDTO(ИдентификаторПодписчикаXML);
	
	Возврат ИдентификаторПодписчикаОбъект;
КонецФункции

Функция ПолучитьИдентификаторПодписчикаXML(МобильноеУстройство) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	nsPushПодписчики.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.nsPushПодписчики КАК nsPushПодписчики
	|ГДЕ
	|	nsPushПодписчики.МобильноеУстройство = &МобильноеУстройство";
	Запрос.УстановитьПараметр("МобильноеУстройство", МобильноеУстройство);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.Идентификатор.Получить();
	КонецЕсли;  	
	
	Возврат Результат;
КонецФункции

#КонецОбласти 